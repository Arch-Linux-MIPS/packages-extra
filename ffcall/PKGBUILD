# $Id: PKGBUILD 203079 2014-01-03 18:03:13Z bpiotrowski $
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Michael Baehr <usemike@spamblocked.com>

pkgname=ffcall
pkgver=1.10
pkgrel=6.2
pkgdesc="C library for implementing foreign function calls in embedded interpreters"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/libffcall/"
license=('GPL2')
options=('!makeflags' 'staticlibs')
source=(http://archlinuxarm.org/builder/src/${pkgname}-${pkgver}.tar.gz)
md5sums=('700e91d12c2fddf3aa4090436ce28370')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  [ "$CARCH" = "x86_64" ] && CONFIGFLAG="--with-pic"
  [ "$CARCH" = "mips32r2el" ] && CONFIGFLAG="--with-pic"
  ./configure --prefix=/usr --mandir=/usr/share/man $CONFIGFLAG
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  #TODO: examine failure
  make check || true
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -d "${pkgdir}"/usr/share/{man,doc/ffcall}
  make DESTDIR="${pkgdir}" htmldir=/usr/share/doc/ffcall install
}
